// Copyright © 2019, Matjaž Guštin
// <dev@matjaz.it> <https://matjaz.it>
// Released under the BSD 3-clause license.

// Loadout 1: ranged, e.g. repeating crossbow
// Loadout 2: melee with dashing shield
// Loadout 3: armor-crushing, e.g. 2x warhammer
// Loadout 4: melee high-damage, e.g. 2x sword

// Loadout 5: 2x vigor wand

// The Ouroboros stone is equipped when nothing
// has to be done to get heal a little.
// The Star stone is equipped to quickly pick
// up materials (wood, stones, tar, bronze).

// FOR GAME VERSION v1.8.6



// --- A simple tank because why not ---
>o-2,0, ┌o┴╪══-
>o-2,1,┌┴──┴─┐
>o-2,2,`@…@…@´



// --- Rocky plateau ---
?loc = rocky
  equip shovel
  ?pickup = stone
    equip star
  // Dysangelos
  ?foe = dysangelos_bearer
    loadout 4
  ?foe = dysangelos_elementalist
    loadout 4
    // TODO use the proper runes against
    // the current rune in the eye of the boss
  ?foe = dysangelos_perfected
    loadout 4
    ?foe.armor > 0
      loadout 3
    ?hp < 31
      // Dsyangleos' most powerful move deals
      // 30 damage points on 5* difficulty
      activate potion
 
 

// --- Deadwood Canyon ---
?loc = valley
  equipR hatchet
  equipL ouroboros
  ?pickup = wood
    equipR star
    equipL ouroboros
  ?foe = mosquito
    loadout 1
  ?foe = scarab
    loadout 4
  ?foe.distance < 10
    loadout 4
  // Xyaloalgia, residue of Leuce
  ?foe = tree_boss
    loadout 4



// --- Caves of Fear ---
?loc = caves
  equipL ouroboros
  ?pickup = tar
    equipR star
    equipL ouroboros
  ?foe = bat
    loadout 1
  ?foe = spider
    loadout 1
  ?foe = eggs
    loadout 1
  ?foe.distance < 10
    loadout 4
  // Bolesh, the cunning
  ?foe = boss
    loadout 4



// --- Mushroom forest ---
?loc = fungus
  equip poison staff
  // ants
  ?foe = ant
    //loadout 1
  // Angry shroom
  ?foe = boss
    equip poison staff
  // Morel, the sporeadic
  ?foe = boss_fat
    //loadout 4
    equip poison staff                          
    ?foe.armor > 0
      loadout 3
  // Enoki, fungi to be with
  ?foe = boss_skinny
    // loadout 4
    equip poison staff



// --- Haunted halls ---
?loc = crypt
  loadout 5
  // Pallas, the skinless
  ?foe = boss
    loadout 5



// --- Boiling mine ---
?loc = mine
  equipL ouroboros
  ?pickup = bronze
    equipR star
    equipL ouroboros
  ?foe = master
    loadout 1
  ?foe = geyser
    loadout 2
  ?foe = elemental
    loadout 1
  ?foe.distance < 10
    loadout 4
  ?foe = walker
    loadout 2
    ?foe.distance < 10
      loadout 3

// --- Bronze guardian ---
?loc = bronze_guardian
  equipL ouroboros
  // Guardian is attackable
  ?time > 600
    loadout 1
  // Guardian hits, we jump on its arm and dash
  ?time > 960 & time < 980
    loadout 2
  // Hitting guardian's face
  ?time > 980
    loadout 4
    // Hitting guardian's face using armor
    // piercing weapons, if the face is covered
    ?foe.armor > 0
      loadout 3
  // Jumping back on the ground
  ?time > 1150
    loadout 1
  // Guardian hits for the second time
  ?time > 1600 & time < 1620
    loadout 2
  // Hitting guardian's face
  ?time > 1620
    loadout 4
    // Hitting guardian's face using armor
    // piercing weapons, if the face is covered
    ?foe.armor > 0
      loadout 3
  // Jumping back on the ground
  ?time > 1770
    loadout 1
  // TODO longer times in higher difficulties



// --- Icy ridge ---
?loc = icy
  equipL ouroboros
  ?foe = elemental
    equip fire staff
  ?foe = pillar
    equip fire staff
  ?foe = ki
    equip fire staff
  ?foe.distance < 10
    equip fire staff
  // Hrímnir
  ?foe = yeti
    ?foe.distance > 2
      // The shovel has an attack range of 1
      // so it forces the player to move very
      // very close. So close that it can avoid
      // the attacks (snowballs)
      equip shovel
    ?foe.distance > 3
      equip dashing shield
    ?foe.distance < 3
      loadout 4



// --- Temple ---
?loc = temple
  equip ice staff



// --- Nagaraja ---
?loc = nagaraja
  loadout 4



// --- General rules ---
// Apply health potion on low health
?hp < 5
  activate potion

// Equip Experience stone to kill a boss
// and get double XP from the kill
?foe = boss & foe.armor = 0 & foe.hp < 25
  equipR sword
  equipL experience
